#!%TCLSH%


#
# Script pour lister les machines d'un ou plusieurs domaines
#
# Called by: script consultmx (page lib/consultmx.htgt)
#
# Parameters (form or url):
#   - critères de sélection : 
#	- domaine : liste de domaines pour lesquels on liste les MX
#   - format de sortie
#	- format : "Consulter" ou "Imprimer"
#
# History
#   2002/05/25 : pda      : design
#   2002/07/09 : pda      : add nologin
#   2002/10/24 : pda      : fichier à trou commun avec d'autres listes
#   2003/05/13 : pda/jean : use auth base
#


#
#


#
# Template pages used by this script
#

set conf(liste)		listedes.html
set conf(listetex)	listedes.tex
set conf(err)		erreur.html

#
# Next actions
# 

# none (no-return page)

#
# Script parameters
#

set conf(form)		{
	{domaine	0 99999}
	{format		1 1}
}

#
# tabular specification for result
# Colonnes :
#	- machine
#	- priorité
#	- MX
#

set conf(tableau) {
    global {
	chars {12 normal}
	columns {45 10 45}
	botbar {yes}
	align {left}
    }
    pattern {Titre} {
	title {yes}
	topbar {yes}
	chars {12 bold}
	align {center}
	vbar {yes}
	colonne { }
	vbar {yes}
	colonne { }
	vbar {yes}
	colonne { }
	vbar {yes}
    }
    pattern {Domaine} {
	chars {12 bold}
	align {center}
	vbar {yes}
	colonne {
	    multicolumn {3}
	}
	vbar {yes}
    }
    pattern {PasdeMX} {
	align {left}
	vbar {yes}
	colonne {
	    multicolumn {3}
	}
	vbar {yes}
    }
    pattern {MX} {
	vbar {yes}
	colonne {
	    botbar {no}
	}
	vbar {yes}
	colonne {
	    align {right}
	}
	vbar {yes}
	colonne { }
	vbar {yes}
    }
    pattern {DernierMX} {
	vbar {yes}
	colonne { }
	vbar {yes}
	colonne {
	    align {right}
	}
	vbar {yes}
	colonne { }
	vbar {yes}
    }
}


#
#

source %LIBDNS%

#
#

# ::webapp::cgidebug ; exit


##############################################################################
# Main procedure
##############################################################################

proc main {} {
    global conf

    #
    # Initialization
    #

    ::dnscontext create d
    d init-cgi "admin" $conf(err) "admin" $conf(form) ftab dbfd login tabuid

    #
    # Analyse et validation des arguments
    #

    # format de sortie
    switch -- [lindex $ftab(format) 0] {
	Consulter	{ set format html }
	Imprimer	{ set format latex }
    }

    foreach d $ftab(domaine) {
	set iddom [read-domain $dbfd $d]
	if {$iddom == -1} then {
	    d error "Domaine '$d' non trouvé"
	}
	set tabdom($d) $iddom
    }

    #
    # Lister les MX pour chaque domaine
    #

    set donnees {}
    lappend donnees {Titre Nom Priorité MX}
    foreach d [lsort $ftab(domaine)] {
	lappend donnees [list Domaine $d]

	set iddom $tabdom($d)

	#
	# Deux variables intermédiaires
	# - tabmx(machin.u-strasbg.fr) = {{10 isis.u-strasbg.fr} {20 amon...}}
	#
	catch {unset tabmx}

	#
	# Récupération des MX de la zone, et stockage dans les
	# deux variables intermédiaires. Le passage par ces deux
	# variables est nécessaire pour pouvoir distinguer le
	# dernier MX et le sortir différemment dans le tableau.
	#

	set sql "SELECT r1.nom || '.' || d1.nom AS gauche,
			m.priorite,
			r2.nom || '.' || d2.nom AS droite
		    FROM dns.rr r1, dns.domaine d1, dns.rr_mx m, dns.rr r2, dns.domaine d2
		    WHERE r1.iddom = d1.iddom
			AND r1.idrr = m.idrr
			AND m.mx = r2.idrr
			AND r2.iddom = d2.iddom
			AND r1.iddom = $iddom
		    ORDER BY r1.nom ASC, m.priorite ASC"
	pg_select $dbfd $sql tab {
	    set gauche $tab(gauche)
	    set prio   $tab(priorite)
	    set droite $tab(droite)
	    lappend tabmx($gauche) [list $prio $droite]
	}

	#
	# Traiter chaque MX de la zone
	#

	set lmx [lsort [array names tabmx]]
	if {[llength $lmx] == 0} then {
	    lappend donnees [list PasdeMX "(pas de MX pour ce domaine)"]
	} else {
	    foreach mx [lsort [array names tabmx]] {
		set n [llength $tabmx($mx)]
		for {set i 0} {$i < $n} {incr i} {

		    set prio   [lindex [lindex $tabmx($mx) $i] 0]
		    set droite [lindex [lindex $tabmx($mx) $i] 1]

		    # sortir le nom du MX seulement la première fois
		    if {$i == 0} then {
			set nom $mx
		    } else {
			set nom ""
		    }

		    # ne pas afficher de trait horizontal sous le
		    # nom du MX, sauf pour le dernier.
		    if {$i < $n - 1} then {
			set pattern MX
		    } else {
			set pattern DernierMX
		    }

		    lappend donnees [list $pattern $nom $prio $droite]
		}
	    }
	}
    }

    #
    # Générer le zoli tableau
    #

    set tableau [::arrgen::output $format $conf(tableau) $donnees]


    #
    # End of script: output page and close database
    #

    set datefmt [dnsconfig get "datefmt"]
    set date  [clock format [clock seconds] -format $datefmt]
    switch -- $format  {
	html	{
	    d result $conf(liste) [list \
					[list %TITRE%	"MX"] \
					[list %TXT%	""] \
					[list %TABLEAU%	$tableau] \
					[list %DATE%	$date] \
				    ]
	}
	latex	{
	    d result $conf(listetex) [list \
					[list %ORIENTATION% "portrait"] \
					[list %TITRE%	"MX"] \
					[list %TXT%	""] \
					[list %TABLEAU%	$tableau] \
					[list %DATE%	$date] \
				    ]
	}
    }
}

::webapp::cgi-exec main %DEBUG%
