#!%TCLSH%


#
# Script pour présenter la page d'ajout d'une machine/alias
#
# Appelé par : index.htgt ou script liste ou script traiteajout
#
# Paramètres (formulaire ou URL) :
#   - lorsque appelé depuis l'accueil : aucun
#   - lorsque appelé depuis la carte des adresses libres (script liste)
#	- adr : adresse IP
#	- next : paramètres pour la suite
#   - lorsque appelé depuis la carte des adresses libres (script traiteajout)
#	XXX
#
#
# Historique
#   2002/04/11 : pda/jean : création
#   2002/05/03 : pda/jean : séparation dans une page particulière
#   2002/07/09 : pda      : ajout de nologin
#   2003/05/13 : pda/jean : utilisation de la base d'authentification
#   2005/04/08 : pda/jean : ajout des profils DHCP
#   2008/07/23 : pda/jean : ajout du droit d'émission SMTP
#   2009/12/31 : pda      : ajout de l'ajout multiple
#   2010/10/08 : pda      : ajout du cas de suite de script
#   2010/10/31 : pda      : ajout ttl
#

set conf(homeurl)	%HOMEURL%

#
# Chemins utilisés par les scripts
#

set conf(lib)		%DESTDIR%/lib
set conf(libdns)	$conf(lib)/libdns.tcl

#
# Définition des noms des pages "à trous"
#

set conf(page)		$conf(lib)/ajout.html
set conf(pageadr)	$conf(lib)/ajout-adr.html
set conf(err)		$conf(lib)/erreur.html

#
# Quelques paramètres du script
#

# les champs discriminants pour la suite
set conf(form)	{
	{adr		0 1}
	{nextprog	0 1}
	{nextargs	0 1}
}

#
# Les outils du parfait concepteur de pages Web dynamiques...
#

lappend auto_path %PKGTCL%
package require webapp
package require pgsql

#
# On y va !
#

# ::webapp::cgidebug ; exit

source $conf(libdns)

##############################################################################
# Programme principal
##############################################################################

proc main {} {
    global conf

    #
    # Initialisation
    #

    ::dnscontext create d
    d init-cgi $conf(err) "" $conf(form) ftab dbfd login tabcor

    #
    # Récupération des types de machines
    #

    set menuhinfo [menu-hinfo $dbfd hinfo ""]

    #
    # Récupération du droit SMTP pour afficher ou non le bouton
    # d'autorisation d'émettre en SMTP non authentifié
    #

    set droitsmtp [menu-droitsmtp $dbfd "droitsmtp" $tabcor(idcor) 0]
    set droitsmtplibelle [lindex $droitsmtp 0]
    set droitsmtpmenu    [lindex $droitsmtp 1]

    #
    # Récupération du droit TTL pour afficher ou non le champ
    # d'édition du TTL
    #

    set ttl [menu-ttl $dbfd "ttl" $tabcor(idcor) ""]
    set ttllibelle [lindex $ttl 0]
    set ttlval     [lindex $ttl 1]

    #
    # Récupération des domaines auxquels le correspond a accès
    # et construction du code HTML approprié.
    #

    set domaine    [menu-domaine $dbfd $tabcor(idcor) domaine    "" "" $conf(err)]
    set domaineref [menu-domaine $dbfd $tabcor(idcor) domaineref "" "" $conf(err)]

    #
    # Récupérer les informations de profil DHCP si besoin est
    #

    set dhcpprofil [menu-dhcpprofil $dbfd "iddhcpprofil" $tabcor(idcor) 0]
    set dhcpprofillibelle [lindex $dhcpprofil 0]
    set dhcpprofilmenu    [lindex $dhcpprofil 1]

    #
    # Récupérer les plages sous forme d'une liste déroulante
    #

    set lres [liste-reseaux $dbfd $tabcor(idgrp) "consult"]
    set taille [llength $lres]
    if {$taille == 0} then {
	set menuplage "Aucun réseau sélectionnable"
    } else {
	set menuplage [::webapp::form-menu "plage" 1 0 $lres {}]
    }

    #
    # Cas d'une adresse déjà fournie
    #

    set adr [string trim [lindex $ftab(adr) 0]]
    set next ""
    if {[string equal $adr ""]} then {
	# show rest of page
	set display "block"
    } else {
	# hide rest of page
	set display "none"
	set nextprog [lindex $ftab(nextprog) 0]
	set nextargs [lindex $ftab(nextargs) 0]
	if {! [string equal $nextprog ""]} then {
	    set n1 "nextprog=[::webapp::post-string $nextprog]"
	    set n2 "nextargs=[::webapp::post-string $nextargs]"
	    set next "?$n1&$n2"
	}
    }

    #
    # Sortie du résultat
    #

    ::webapp::send html [::webapp::file-subst $conf(page) \
				    [list \
					    [list %DOMAINE% $domaine] \
					    [list %DOMAINEREF% $domaineref] \
					    [list %MENUHINFO% $menuhinfo] \
					    [list %TTLLIBELLE% $ttllibelle] \
					    [list %TTLVAL% $ttlval] \
					    [list %DROITSMTPLIBELLE% $droitsmtplibelle] \
					    [list %DROITSMTPMENU% $droitsmtpmenu] \
					    [list %DHCPPROFILLIBELLE% $dhcpprofillibelle] \
					    [list %DHCPPROFILMENU% $dhcpprofilmenu] \
					    [list %MENUPLAGE% $menuplage] \
					    [list %DISPLAY% $display] \
					    [list %ADR% $adr] \
					    [list %NEXT% $next] \
					] \
				]

    #
    # Déconnexion de la base
    #

    fermer-base $dbfd
}

::webapp::cgi-exec main %DEBUG%
